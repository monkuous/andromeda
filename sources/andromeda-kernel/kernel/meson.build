script = files('kernel.lds')[0]

config_data = configuration_data()
config_data.set_quoted('ANDROMEDA_RELEASE', meson.project_version())
config_data.set_quoted('ANDROMEDA_VERSION', '@VCS_TAG@')

config = vcs_tag(
    input: configure_file(configuration: config_data, output: 'config.h.in'),
    output: 'config.h',
)

kernel_elf = executable(
    'kernel.elf',
    'cpu/gdt.c',
    'cpu/idt.c',
    'cpu/idt.S',
    'drv/biosdisk.c',
    'drv/console/console.c',
    'drv/console/screen.c',
    'drv/console/vt.c',
    'drv/cpu.c',
    'drv/cpu.S',
    'drv/device.c',
    'drv/idle.c',
    'drv/loopback.c',
    'drv/mem.c',
    'drv/partition.c',
    'fs/detect.c',
    'fs/fat.c',
    'fs/fifo.c',
    'fs/iso9660.c',
    'fs/pgcache.c',
    'fs/ramfs.c',
    'fs/vfs.c',
    'init/main.c',
    'init/main.S',
    'mem/bootmem.c',
    'mem/kmalloc.c',
    'mem/memdetect.c',
    'mem/pmap.c',
    'mem/pmem.c',
    'mem/usermem.S',
    'mem/vmalloc.c',
    'mem/vmem.c',
    'mem/vmm.c',
    'proc/exec.c',
    'proc/process.c',
    'proc/sched.c',
    'proc/signal.c',
    'sys/fs.c',
    'sys/memory.c',
    'sys/misc.c',
    'sys/process.c',
    'sys/sched.c',
    'sys/syscall.c',
    'sys/system.c',
    'sys/thread.c',
    'util/panic.c',
    'util/print.c',
    'util/reset.c',
    'util/string.c',
    config,
    build_by_default: false,
    c_args: [
        '-D_GNU_SOURCE',
        '-ffreestanding',
        '-fno-asynchronous-unwind-tables',
        '-fno-pie',
        '-fno-stack-protector',
        '-mgeneral-regs-only',
        '-mregparm=3',
    ],
    link_args: ['-nostdlib', '-static', '-T' + script.full_path()],
    link_depends: script,
)

custom_target(
    'kernel.sys',
    build_by_default: true,
    command: [objcopy, '@INPUT@', '@OUTPUT@', '-Obinary'],
    input: kernel_elf,
    install: true,
    install_dir: '/boot',
    install_mode: 'rw-r--r--',
    output: 'andromed.sys',
)
